GET_ASSET_TYPES:
  comment: Retrieve Asset description in getSystemAssetClasses method
  sparql: |
    PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
    PREFIX core:<http://it-innovation.soton.ac.uk/ontologies/trustworthiness/core#>
    SELECT DISTINCT ?asset ?al (GROUP_CONCAT(?c;separator=",") AS ?type) ?category ?description ?a (STR(?minCardinality) AS ?min) (STR(?maxCardinality) AS ?max)
    WHERE {
      GRAPH <http://it-innovation.soton.ac.uk/ontologies/trustworthiness/domain/> {
        ?c rdfs:subClassOf+ core:Asset .
        ?asset rdfs:subClassOf* ?c .
        ?asset rdfs:label ?al .
        ?asset rdfs:subClassOf* ?category .
        ?category rdfs:subClassOf core:Asset .
        OPTIONAL { ?asset rdfs:comment ?description }
        OPTIONAL { ?asset core:isAssertable ?assertable }
        OPTIONAL { ?asset core:minCardinality ?minCardinality }
        OPTIONAL { ?asset core:maxCardinality ?maxCardinality }
        BIND(IF(BOUND(?assertable),STR(?assertable),"false") AS ?a)
      }
      FILTER(!isBlank(?asset))
      FILTER(!isBlank(?category))
    } GROUP BY ?asset ?al ?category ?description ?a ?minCardinality ?maxCardinality ORDER BY ?type ?asset

GET_RELATION_TYPES:
  comment: Retrieve all relations and their domain and range
  sparql: |
    PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
    PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
    PREFIX core:<http://it-innovation.soton.ac.uk/ontologies/trustworthiness/core#>
    PREFIX owl:<http://www.w3.org/2002/07/owl#>
    SELECT DISTINCT ?from ?prop ?label ?to WHERE {
    GRAPH <http://it-innovation.soton.ac.uk/ontologies/trustworthiness/domain/> {
        ?prop a owl:ObjectProperty .
        ?prop rdfs:domain ?domain .
        ?prop rdfs:range ?range .
        ?prop rdfs:label ?label .
        {
            ?dasset rdfs:subClassOf* core:Asset .
            ?domain rdfs:subClassOf* ?dasset .
            ?from rdfs:subClassOf* ?domain .
        } UNION {
            ?domain owl:unionOf ?du .
            ?du (rdf:rest)*/rdf:first ?da .
            ?from rdfs:subClassOf* ?da .
        }
        {
            ?rasset rdfs:subClassOf* core:Asset .
            ?range rdfs:subClassOf* ?rasset .
            ?to rdfs:subClassOf* ?range .
        } UNION {
            ?range owl:unionOf ?ru .
            ?ru (rdf:rest)*/rdf:first ?ra .
            ?to rdfs:subClassOf* ?ra .
        }
      }
    } ORDER BY ?prop ?from ?to
